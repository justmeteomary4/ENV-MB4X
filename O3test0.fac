EXECUTE OPEN 7 "O3test1_chem.dat" UNKNOWN;
EXECUTE OPEN 8 "O3test1_supp.dat" UNKNOWN;

VARIABLE
O3 O1D O3P OH NO NO2 HO2 H2O2 CH3O CH3O2 CH3OOH CH3NO3 HCHO HCO H H2 O3F;
PARAMETER
O2 5.090E+18
N2 1.897E+19
H2O 0.
CO 2.430E+12
CO2 9.720E+15 
CH4 4.374E+16;
*M 2.430E+19;

* Time: 01.06.15, start 06:00 GMT, end 22:00 GMT (7.920D+4);
PARAMETER TIME0 2.16D+4 TIMEN 7.920D+4;
* Location: 45N 30W, solar declination angle (DEC) [degrees];
PARAMETER LAT 45.0 DEC 23.12 PI XTIME XHOUR LHA SEC;
* Meteorological conditions: clear sky, dry;
PARAMETER TEMP 298;
* Photolysis (data from Jenkin1997);
PARAMETER <3> J;
PARAMETER <3> L 1.748D-6 4.866D-5 6.790D-5 M 1.166 0.781 0.565;
PARAMETER <3> N 0.324 0.349 0.275;
PARAMETER <50> WORK;

COMPILE INITIAL;
FOR #8=0;
O3 = 9.720E+11;
OH = 9.700E+5;
NO = 1.215E+08;
NO2 = 2.430E+11;
?PI=4.*ARTAN(1.0D+0);
**;

COMPILE FTIME;
FOR #8=*;
XTIME=TIME+TIME0;
XHOUR=XTIME/3600;
*Local hour angle (LHA) [radians];
LHA=(1.+XTIME/4.32D+4)*PI;
SEC=1.0D+0/(RAMP(COS(LHA)*COS(LAT)*COS(DEC)+SIN(LAT)*SIN(DEC))+1.0D-30);
ARRAY <3> WORK;
J=L*(COS(DEC*PI/180))@M*EXP(-N/COS(DEC*PI/180));
ARRAY END;
DO 10 FOR #11 = 0(1)3;
  IF (J<#11>-1.0D-9) *,15,15;
    J<#11>=1.0D-9;
  LABEL 15;
LABEL 10;
**;

COMPILE EQUATIONS;
* Experiment without ANs (0 or control);
% 2.0D-6                        : O3 = O1D;
% 2.2D-10                       : O1D+H2O = OH+OH;
% 2.2D-13                       : OH+CO = HO2+CO2;
% 6.3D-15                       : OH+CH4 = CH3O2;
% 1.7D-12                       : HO2+HO2 = H2O2;
% 5.6D-12                       : HO2+CH3O2 = CH3OOH;
% 2.0D-15                       : HO2+O3 = OH;
% 8.1D-12                       : HO2+NO = NO2+OH;
% 7.6D-12                       : CH3O2+NO = NO2+CH3O;
% 1.4D-13                       : CH3O2+CH3O2 = CH3O+CH3O;
% 6.5D-6                        : CH3OOH = CH3O+OH; *hv;
% 5.2D-12                       : CH3OOH+OH = CH3O2;
% 1.0D3                         : NO2 = NO+O3P;
% 1.8D-14                       : O3P+O2 = O3;
* Experiment with ANs (1);
% 2.3D-12*EXP(360/TEMP)*0.001   : CH3O2+NO = CH3NO3; *MCM;
% 1.6D-6                        : CH3NO3 = CH3O+NO2; *Brasseur;
% 4.0D-13*EXP(-845/TEMP)        : CH3NO3+OH = HCHO+NO2; *MCM;
% J<1>                          : HCHO = HCO+H;
% J<2>                          : HCHO = CO+H2;
% 1.1D-11                       : HCHO+OH = HO2+CO; *plumea;
% 5.5D-12                       : HCO+O2 = HO2+CO;
% 2.4D-13                       : CO+OH = CO2+HO2;
*= 1.8D-14                       : = O3F;
**;

WHEN 
1) TIME=0+1*3599% CALL BLOCK 1;
**;
COMPILE BLOCK 1;
#8=#8+1;
**;
COMPILE TABLE;
PSTREAM 3;
PSTREAM 4;
**;
WHENEVER
TIME=0+1*3599 % CALL TABLE;
**;
SETPSTREAM 3 7 14;
TIME O3 O1D O3P OH NO NO2 HO2 H2O2 CH3O CH3O2 CH3OOH CH3NO3 HCHO;
**;
SETPSTREAM 4 8;
XTIME LHA SEC J;
**;
BEGIN;
STOP;