EXECUTE OPEN 7  "chem_1_03_CO.dat" UNKNOWN;
EXECUTE OPEN 8  "chem_1_03_CH4.dat" UNKNOWN;
EXECUTE OPEN 9  "chem_1_03_CH4AN.dat" UNKNOWN;
EXECUTE OPEN 10 "chem_1_03_C2H6.dat" UNKNOWN;
EXECUTE OPEN 11 "chem_1_03_C2H6AN.dat" UNKNOWN;
EXECUTE OPEN 12 "chem_1_03_C3H8.dat" UNKNOWN;
EXECUTE OPEN 13 "chem_1_03_C3H8AN.dat" UNKNOWN;

EXECUTE OPEN 14 "flux_0_01_test.dat" UNKNOWN;
* Time: 01.07.15;
* Location: 45N 30W;
*---------------------------------METEO--------------------------------;
* Meteorological conditions: clear sky, dry, T [K], RH [%];
PARAMETER T 298 RH 80;

*-------------------------------CHEMISTRY------------------------------;
*------------------------VARIABLES AND PARAMETERS----------------------;
* Values are given in number density [molecules cm-3];
*-------------------------------INORGANIC------------------------------;
VARIABLE H2 O O1D O3 OH HO2 H2O2 NO NO2 CO;
PARAMETER H2O M 2.431E+19 O2 5.092E+18 N2 1.898E+19;
PARAMETER
MO3 MO1D MOH MNO MNO2 MHO2 MH2O2 MCO;
*---Nett reaction rate coefficients;
PARAMETER
RI1A RI1B RI2 RI3 RI12A RI12B RI19 RI20 RI23 RI24 RI25 RI26 RI27 RI28 
RI30 RI36 RI45 RI45 RI54 RI68;
*---Fluxes;


PARAMETER KI2627;
*--------------------------------ORGANIC-------------------------------;
VARIABLE RO2;

*---Methane (0_01);
VARIABLE
CH4 HCHO CH3O CH3O2 CH3OOH CH3NO3;
PARAMETER
MCH4 MHCHO MCH3O MCH3O2 MCH3OOH MCH3NO3;

*---Ethane (0_02);
VARIABLE 
C2H5NO3 C2H5O C2H5O2 C2H5OH C2H5OOH C2H6 CH3CHO CH3CO HCOCH2O2 
HOCH2CH2O2;
PARAMETER 
MC2H6 MC2H5O MC2H5O2 MC2H5OOH MCH3CHO MCH3CO MHCOCH2O2 MHOCH2CH2O2 
MC2H5NO3;

*---Propane (0_03);
VARIABLE 
C2H5CHO C2H5CO3 C3H8 CH3CO3 CH3COCH2O2 CH3COCH3 IC3H7NO3 IC3H7O IC3H7O2 
IC3H7OOH NC3H7NO3 NC3H7O NC3H7O2 NC3H7OOH;
PARAMETER 
MC3H8 MIC3H7O MIC3H7O2 MNC3H7O MNC3H7O2 MIC3H7OOH MNC3H7OOH MC2H5CHO 
MC2H5CO3 MCH3CO3 MCH3COCH2O2 MCH3COCH3;

*---Nett reaction rate coefficients;
PARAMETER
RCH41 RCH42 RCH43 RCH44 RCH45 RCH46 RCH47 RCH48 RCH49 RCH410 RCH411 
RCH412;
PARAMETER
RC2H61 RC2H62 RC2H63 RC2H64 RC2H65 RC2H66 RC2H67 RC2H68 RC2H69 RC2H610 
RC2H611 RC2H612 RC2H613 RC2H614 RC2H615 RC2H616;
PARAMETER 
RC3H81 RC3H82 RC3H83 RC3H84 RC3H85 RC3H86 RC3H87 RC3H88 RC3H89 RC3H810 
RC3H811 RC3H812 RC3H813 RC3H814 RC3H815 RC3H816 RC3H817 RC3H818;

*---Nett reaction rate coefficients of reactions with alkyl nitrates;
PARAMETER
RCH4NO31 RCH4NO32 RCH4NO33 
RC2H6NO31 RC2H6NO32 RC2H6NO33
RC3H7NO31 RC3H7NO32 RC3H7NO33 RC3H7NO34 RC3H7NO35 RC3H7NO36;

*---Fluxes-------------------------------------------------------------;
VARIABLE 
FRCH41 FRCH42 FRCH43 FRCH44 FRCH45 FRCH46 FRCH47 FRCH48 FRCH49 FRCH410 
FRCH411 FRCH412 FRCH413 FRCH414 FRCH415;
VARIABLE 
FRC2H61 FRC2H62 FRC2H63 FRC2H64 FRC2H65 FRC2H66 FRC2H67 FRC2H68 FRC2H69 
FRC2H610 FRC2H611 FRC2H612 FRC2H613 FRC2H614 FRC2H615 FRC2H616;
VARIABLE 
FRC3H81 FRC3H82 FRC3H83 FRC3H84 FRC3H85 FRC3H86 FRC3H87 FRC3H88 FRC3H89 
FRC3H810 FRC3H811 FRC3H812 FRC3H813 FRC3H814 FRC3H815 FRC3H816 FRC3H817 
FRC3H818;

VARIABLE 
FRCH4NO31 FRCH4NO32 FRCH4NO33 
FRC2H6NO31 FRC2H6NO32 FRC2H6NO33 
FRC3H7NO31 FRC3H7NO32 FRC3H7NO33 FRC3H7NO34 FRC3H7NO35 FRC3H7NO36;

PARAMETER <57> J 
0.00E+00 
1.71E-05 2.39E-04 3.86E-06 4.39E-03 1.19E-02 8.14E-02 9.67E-04 3.31E-07 
0.00E+00 0.00E+00 1.55E-05 2.38E-05 2.24E-06 9.53E-06 9.50E-06 5.70E-06 
2.60E-05 5.34E-07 5.34E-07 2.74E-04 2.99E-07 1.87E-06 8.76E-07 8.76E-07 
0.00E+00 0.00E+00 0.00E+00 0.00E+00 0.00E+00 0.00E+00 2.84E-05 4.29E-06 
1.34E-05 6.32E-05 1.35E-04 0.00E+00 0.00E+00 0.00E+00 0.00E+00 0.00E+00 
2.83E-06 0.00E+00 0.00E+00 0.00E+00 0.00E+00 0.00E+00 0.00E+00 0.00E+00 
0.00E+00 0.00E+00 5.56E-07 6.52E-07 8.58E-07 1.44E-06 4.03E-06 1.52E-05;

COMPILE INITIAL;
H2 = 1.276E+13;
O3 = 9.722E+11;
NO = 1.215E+8;
NO2 = 2.431E+7;
CO = 2.431E+12; 
CH4 = 4.375E+13;
C2H6 = 2.431E+10;
C3H8 = 2.431E+10;
**;

* Generic Rate Coefficients;
PARAMETER
KRO2NO KRO2HO2 KAPHO2 KAPNO KRO2NO3 KNO3AL KDEC KROPRIM KROSEC KCH3O2 
K298CH3O2 K16ISOM1 K15ISOM1 K14ISOM1;

COMPILE INSTANT;
KRO2NO = 2.7D-12*EXP(360/T);
KRO2HO2 = 2.91D-13*EXP(1300/T);
KAPHO2 = 5.2D-13*EXP(980/T);
KAPNO = 7.5D-12*EXP(290/T);
KRO2NO3 = 2.3D-12;
KNO3AL = 1.4D-12*EXP(-1860/T);
KDEC = 1.00D+06;
KROPRIM = 2.50D-14*EXP(-300/T);
KROSEC = 2.50D-14*EXP(-300/T);
KCH3O2 = 1.03D-13*EXP(365/T);
K298CH3O2 = 3.5D-13;
K16ISOM1 = 4.60D9*EXP(-8380/T)*EXP(1.00D8/T@3);
K15ISOM1 = 1.50D11*EXP(-9750/T);
K14ISOM1 = 3.00D7*EXP(-5300/T);
**;

COMPILE INSTANT;
H2O = 6.1078*EXP(-1.0D0*(597.3-0.57*(T-273.16))*
18./1.986*(1./T-1./273.16))*10./(1.38D-16*T)*RH;
KI2627 = 1+(1.4D-21*EXP(2200/T)*H2O);
**;

COMPILE EQUATIONS;
*-------------------------------REACTIONS------------------------------;
*-------------------------------INORGANIC------------------------------;
*---Inorganic (0_00);
RI1A  % 6.0D-34*O2*(T/300)@-2.6*O2  : O = O3;
RI1B  % 5.6D-34*N2*(T/300)@-2.6*O2  : O = O3;
RI2   % 8.0D-12*EXP(-2060/T)        : O+O3 = ;
RI3   % 3.2D-11*EXP(67/T)*O2        : O1D = O;
RI12A % J<1>                        : O3 = O1D; 
RI12B % J<2>                        : O3 = O;
RI19  % 2.2D-10*H2O                 : O1D = OH+OH;
RI20  % 7.7D-12*EXP(-2100/T)        : OH+H2 = HO2;
RI23  % 4.8D-11*EXP(250/T)          : OH+HO2 = ;
RI24  % 2.9D-12*EXP(-160/T)         : OH+H2O2 = HO2;
RI25  % 1.7D-12*EXP(-940/T)         : OH+O3 = HO2;
RI26  % 2.2D-13*EXP(600/T)*KI2627   : HO2+HO2 = H2O2;
RI27  % 1.9D-33*N2*EXP(980/T)*KI2627: HO2+HO2 = H2O2;
RI28  % 2.03D-16*(T/300)@4.57*EXP(693/T): HO2+O3 = OH;
RI30  % J<3>                        : H2O2 = OH+OH;
RI36  % 1.8D-11*EXP(107/T)*N2       : O1D = O;
RI45  % 3.6D-12*EXP(270/T)          : HO2+NO = OH+NO2;
RI54  % 1.4D-12*EXP(-1310/T)        : NO+O3 = NO2;
RI68  % J<4>                        : NO2 = NO+O;
*--------------------------------ORGANIC-------------------------------;
*---Methane (0_01);
RCH41 % 1.85D-12*EXP(-1690/T)       : OH+CH4 = CH3O2; 
RCH42 % 1.44D-13*(1+(M/4.2D+19))    : OH+CO = HO2; 
RCH43 % 5.4D-12*EXP(135/T)          : OH+HCHO = HO2+CO; 
*RCH4? % 2.85D-12*EXP(-345/T)        : OH+CH3OH = HO2+HCHO; 
RCH44 % 5.3D-12*EXP(190/T)*0.6      : OH+CH3OOH = CH3O2; 
RCH45 % 5.3D-12*EXP(190/T)*0.4      : OH+CH3OOH = HCHO+OH;    
RCH46 % 3.8D-13*EXP(780/T)*(1-1/(1+498*EXP(-1160/T))):HO2+CH3O2=CH3OOH; 
RCH47 % 3.8D-13*EXP(780/T)*(1/(1+498*EXP(-1160/T)))  :HO2+CH3O2=HCHO; 
RCH48 % 7.2D-14*EXP(-1080/T)*O2     : CH3O = HCHO+HO2;
RCH49 % 2.3D-12*EXP(360/T)*0.999    : CH3O2+NO = CH3O+NO2; 
RCH410% J<11>                       : HCHO = CO+HO2+HO2; 
RCH411% J<12>                       : HCHO = CO+H2; 
RCH412% J<41>                       : CH3OOH = CH3O+OH; 
*---Ethane (0_02);
RC2H61 % 6.9D-12*EXP(-1000/T)        : OH+C2H6 = C2H5O2;
RC2H62 % 3.8D-13*EXP(900/T)          : HO2+C2H5O2 = C2H5OOH;
RC2H63 % 2.55D-12*EXP(380/T)*0.991   : C2H5O2+NO = C2H5O+NO2;
RC2H67 % J<41>                       : C2H5OOH = C2H5O+OH;
RC2H68 % 1.90D-12*EXP(190/T)         : OH+C2H5OOH = C2H5O2;
RC2H69 % 8.01D-12                    : OH+C2H5OOH = CH3CHO+OH;
RC2H610% 2.4D-14*EXP(-325/T)*O2      : C2H5O = CH3CHO+HO2;
RC2H611% 3.0D-12*EXP(20/T)*0.05      : OH+C2H5OH = C2H5O;
RC2H612% 3.0D-12*EXP(20/T)*0.90      : OH+C2H5OH = CH3CHO+HO2;
RC2H613% 3.0D-12*EXP(20/T)*0.05      : OH+C2H5OH = HOCH2CH2O2;
RC2H614% J<13>                       : CH3CHO = CH3O2+HO2+CO;
RC2H615% 4.7D-12*EXP(345/T)*0.95     : OH+CH3CHO = CH3CO;
RC2H616% 4.7D-12*EXP(345/T)*0.05     : OH+CH3CHO = HCOCH2O2;
*---Propane (0_03);
RC3H81 % 7.6D-12*EXP(-585/T)*0.736   : OH+C3H8 = IC3H7O2;
RC3H82 % 7.6D-12*EXP(-585/T)*0.264   : OH+C3H8 = NC3H7O2;
RC3H83 % KRO2HO2*0.520               : IC3H7O2+HO2 = IC3H7OOH;
RC3H84 % 2.7D-12*EXP(360/T)*0.958    : IC3H7O2+NO = IC3H7O+NO2;
RC3H85 % KRO2HO2*0.520               : NC3H7O2+HO2 = NC3H7OOH;
RC3H86 % 2.9D-12*EXP(350/T)*0.980    : NC3H7O2+NO = NC3H7O+NO2;
RC3H87 % J<41>                       : IC3H7OOH = IC3H7O+OH;
RC3H88 % 1.66D-11                    : OH+IC3H7OOH = CH3COCH3+OH;
RC3H89 % 1.90D-12*EXP(190/T)         : OH+IC3H7OOH = IC3H7O2;
RC3H810% 1.5D-14*EXP(-230/T)*O2      : IC3H7O = CH3COCH3+HO2;
RC3H811% 8.8D-12*EXP(-1320/T)+1.7D-14*EXP(423/T):CH3COCH3+OH=CH3COCH2O2; 
RC3H812% J<21>                       : CH3COCH3 = CH3CO3+CH3O2;
RC3H813% J<41>                       : NC3H7OOH = NC3H7O+OH;
RC3H814% 1.10D-11                    : OH+NC3H7OOH = C2H5CHO+OH;
RC3H815% 1.90D-12*EXP(190/T)         : OH+NC3H7OOH = NC3H7O2;
RC3H816% 2.6D-14*EXP(-255/T)*O2      : NC3H7O = C2H5CHO+HO2;
RC3H817% 4.9D-12*EXP(405/T)          : C2H5CHO+OH = C2H5CO3;
RC3H818% J<14>                       : C2H5CHO = C2H5O2+HO2+CO;
*-------------------------------ALKYL NITRATES-------------------------;
*---Methyl nitrate (1_01);
RCH4NO31 % 2.3D-12*EXP(360/T)*0.001    : CH3O2+NO = CH3NO3;
RCH4NO32 % 4.0D-13*EXP(-845/T)         : OH+CH3NO3 = HCHO+NO2;
RCH4NO33 % J<51>                       : CH3NO3 = CH3O+NO2; 
*---Ethyl nitrate (1_02);
RC2H6NO31% 2.55D-12*EXP(380/T)*0.009   : C2H5O2+NO = C2H5NO3;
RC2H6NO32% 6.7D-13*EXP(-395/T)         : OH+C2H5NO3 = CH3CHO+NO2;
RC2H6NO33% J<52>                       : C2H5NO3 = C2H5O+NO2;
*---Propyl nitrate (1_03);
RC3H7NO31% 2.7D-12*EXP(360/T)*0.042      : IC3H7O2+NO = IC3H7NO3;
RC3H7NO32% 6.2D-13*EXP(-230/T)           : OH+IC3H7NO3 = CH3COCH3+NO2;
RC3H7NO33% J<54>                         : IC3H7NO3 = IC3H7O+NO2;
RC3H7NO34% 2.9D-12*EXP(350/T)*0.020      : NC3H7O2+NO = NC3H7NO3;
RC3H7NO35% 5.8D-13                       : OH+NC3H7NO3 = C2H5CHO+NO2;
RC3H7NO36% J<53>                         : NC3H7NO3 = NC3H7O+NO2;
*----------------------------------FLUXES------------------------------;
= RCH41  : = FRCH41;
= RCH42  : = FRCH42;
= RCH43  : = FRCH43;
= RCH44  : = FRCH44;
= RCH45  : = FRCH45;
= RCH46  : = FRCH46;
= RCH47  : = FRCH47;
= RCH48  : = FRCH48;
= RCH49  : = FRCH49;
= RCH410 : = FRCH410;
= RCH411 : = FRCH411;
= RCH412 : = FRCH412;

= RCH4NO31 : = FRCH4NO31;
= RCH4NO32 : = FRCH4NO32;
= RCH4NO33 : = FRCH4NO33;
**;

WHEN
* TIME=0+3600*24 CALL BLOCK 1; *every hour;
TIME=0+1*86400 % CALL BLOCK 1;
**;

COMPILE BLOCK 1;
* Convert number density to mixing ratio [ppb];
MCO = CO/M*1.0D+9;
MO1D = O1D/M*1.0D+9;
MO3 = O3/M*1.0D+9;
MOH = OH/M*1.0D+9;
MHO2 = HO2/M*1.0D+9;
MH2O2 = H2O2/M*1.0D+9;
MNO = NO/M*1.0D+9;
MNO2 = NO2/M*1.0D+9;

MCH4 = CH4/M*1.0D+9;
MHCHO = HCHO/M*1.0D+9;
MCH3O = CH3O/M*1.0D+9;
MCH3O2 = CH3O2/M*1.0D+9;
MCH3OOH = CH3OOH/M*1.0D+9;
MCH3NO3 = CH3NO3/M*1.0D+9;

MC2H6 = C2H6/M*1.0D+9;
MC2H5O = C2H5O/M*1.0D+9;
MC2H5O2 = C2H5O2/M*1.0D+9;
MC2H5OOH = C2H5OOH/M*1.0D+9;
MCH3CHO = CH3CHO/M*1.0D+9;
MCH3CO = CH3CO/M*1.0D+9;
MHCOCH2O2 = HCOCH2O2/M*1.0D+9;
MHOCH2CH2O2 = HOCH2CH2O2/M*1.0D+9;
MC2H5NO3 = C2H5NO3/M*1.0D+9;

MC3H8 = C3H8/M*1.0D+9;
MIC3H7O = IC3H7O/M*1.0D+9;
MIC3H7O2 = IC3H7O2/M*1.0D+9;
MNC3H7O = NC3H7O/M*1.0D+9;
MNC3H7O2 = NC3H7O2/M*1.0D+9;
MIC3H7OOH = IC3H7OOH/M*1.0D+9;
MNC3H7OOH = NC3H7OOH/M*1.0D+9;
MC2H5CHO = C2H5CHO/M*1.0D+9;
MC2H5CO3 = C2H5CO3/M*1.0D+9;
MCH3CO3 = CH3CO3/M*1.0D+9;
MCH3COCH2O2 = CH3COCH2O2/M*1.0D+9;
MCH3COCH3 = CH3COCH3/M*1.0D+9;
MIC3H7NO3 = IC3H7NO3/M*1.0D+9;
MNC3H7NO3 = NC3H7NO3/M*1.0D+9;
**;

*------------------------------OUTPUT----------------------------------;
COMPILE TABLE1; * CO;
WRITE 3=7,(N8(E14,6)),MO3,MO1D,MOH,MNO,MNO2,MHO2,MH2O2,MCO %;
**;
COMPILE TABLE2; * CH4;
WRITE 3=8,(N5(E14,6)),MCH4,MHCHO,MCH3O,MCH3O2,MCH3OOH %;
**;
COMPILE TABLE3; * CH4AN;
WRITE 3=9,(N1(E14,6)),MCH3NO3 %;
**;
COMPILE TABLE4; * C2H6;
WRITE 3=10,(N8(E14,6)),MC2H6,MC2H5O,MC2H5O2,MC2H5OOH,MCH3CHO,MCH3CO,
MHCOCH2O2,MHOCH2CH2O2 %;
* ;
**;
COMPILE TABLE5; * C2H6AN;
WRITE 3=11,(N1(E14,6)),MC2H5NO3 %;
**;
COMPILE TABLE6; * C3H8;
WRITE 3=12,(N8(E14,6)),MC3H8,MIC3H7O,MIC3H7O2,MNC3H7O,MNC3H7O2, 
MIC3H7OOH,MNC3H7OOH,MC2H5CHO %;
**;
COMPILE TABLE7; * C3H8AN;
WRITE 3=13,(N6(E14,6)),MC2H5CO3,MCH3CO3,MCH3COCH2O2,MCH3COCH3,IC3H7NO3,
NC3H7NO3 %;
**;

WHENEVER
TIME=0+1*86400 % CALL TABLE1;
TIME=0+1*86400 % CALL TABLE2;
TIME=0+1*86400 % CALL TABLE3;
TIME=0+1*86400 % CALL TABLE4;
TIME=0+1*86400 % CALL TABLE5;
TIME=0+1*86400 % CALL TABLE6;
TIME=0+1*86400 % CALL TABLE7;
**;

* SETPSTREAM 4 8 18;
* FRCH41 FRCH42 FRCH43 FRCH44 FRCH45 FRCH46 FRCH47 FRCH48 FRCH49 FRCH410 
FRCH411 FRCH412 FRCH413 FRCH414 FRCH415 FRCH4NO31 FRCH4NO32 FRCH4NO33;

BEGIN;
STOP;