EXECUTE OPEN 7 "chem_1.dat" UNKNOWN;
EXECUTE OPEN 8 "supp_1.dat" UNKNOWN;

VARIABLE
H H2 
O O1D O3P OH O3 
NO NO2 
HO2 H2O2 
CH3O CH3O2 CH3OH CH3OOH CH3NO3 HCHO HCO 
O3F;
PARAMETER
O2 5.090E+18
N2 1.897E+19
H2O 0.
CO 2.430E+12
CO2 9.720E+15 
CH4 4.374E+16;
*M 2.430E+19;

* Time: 01.07.15;
* Location: 45N 30W;
PARAMETER LAT 45.0;
* Meteorological conditions: clear sky, dry;
PARAMETER TEMP 298;

COMPILE INITIAL;
FOR #8=0;
O3 = 9.720E+11;
OH = 9.700E+5;
NO = 1.215E+08;
NO2 = 2.430E+11;
**;

COMPILE FTIME;
FOR #8=*;
**;

COMPILE EQUATIONS;
* Experiment without ANs (0 or control);
% 1.71D-5                     : O3 = O1D;                 *J<0> 1.71D-5 24hour average MCMv3.3, 2.0D-6 ANsNEW;
% 2.2D-10                     : O1D+H2O = OH+OH;          *Atkinson2004;
* % 2.2D-13                     : OH+CO = HO2+CO2;          *plumea;
% 1.44D-13*(1+N2/4.2D19)      : OH+CO = H+CO2;            *Atkinson2004;
RF % 6.3D-15                     : OH+CH4 = CH3O2;           *BrasseurTextbook1999 net from 2 reactions;
% 1.6D-12                     : HO2+HO2 = H2O2;           *Atkinson2004;
% 5.2D-12                     : HO2+CH3O2 = CH3OOH;       *Atkinson2006;
% 2.0D-15                     : HO2+O3 = OH;              *Atkinson2004;
% 8.8D-12                     : HO2+NO = NO2+OH;          *Atkinson2004;
% 1.8D-14                     : NO+O3 = NO2;              *Atkinson2004;
% 7.7D-12                     : CH3O2+NO = NO2+CH3O;      *Atkinson2006;
% 1.3D-13                     : CH3O2+CH3O2 = CH3OH+HCHO; *Atkinson2006;
% 1.3D-13*0.30                : CH3O2+CH3O2 = CH3O+CH3O;  *Atkinson2006;
% 6.5D-6                      : CH3OOH = CH3O+OH;         *J<1> BrasseurTextbook1999;
% 5.2D-12                     : CH3OOH+OH = CH3O2;        *BrasseurTextbook1999;
% 1.0D-3                      : NO2 = NO+O3P;             *J<2> 1.0D-3 ANsNEW;
% 5.6D-34*N2                  : O+O2 = O3;                *Atkinson2004;
* Experiment with ANs (1);
% 2.3D-12*EXP(360/TEMP)*0.999 : CH3O2+NO = CH3O+NO2;      *MCMv3.3;
% 2.3D-12*EXP(360/TEMP)*0.001 : CH3O2+NO = CH3NO3;        *MCMv3.3;
% 5.6D-7                      : CH3NO3 = CH3O+NO2;        *J<3> 5.6D-7 24hour average MCMv3.3, 1.6D-6 BrasseurTextbook1999;
% 2.3D-14                     : CH3NO3+OH = HCHO+NO2;     *Atkinson2006 and MCMv3.3;
% 1.55D-5                     : HCHO = HCO+H;             *J<4> MCMv3.3;
% 2.38D-5                     : HCHO = CO+H2;             *J<5> MCMv3.3;
% 8.5D-12                     : HCHO+OH = HCO;            *Atkinson2006;
% 5.1D-12                     : HCO+O2 = CO+HO2;          *Atkinson2006;
= RF                       : = O3F;
**;

WHEN
* TIME=0+3600*24 CALL BLOCK 1; *every hour;
* TIME=0+60*1440 CALL BLOCK 2; *every minute;
TIME=0+1*86400 % CALL BLOCK 4;
**;
COMPILE BLOCK 4;
#8=#8+1;
**;
COMPILE TABLE;
PSTREAM 3;
PSTREAM 4;
**;
WHENEVER
TIME=0+1*86400 % CALL TABLE;
**;
SETPSTREAM 3 7 15;
TIME O3 O1D O3P OH NO NO2 HO2 H2O2 CH3O CH3O2 CH3OOH CH3NO3 HCHO O3F;
**;
SETPSTREAM 4 8;
TIME;
**;
BEGIN;
STOP;